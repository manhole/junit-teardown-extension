import java.time.Instant
import java.time.temporal.ChronoUnit

plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id "com.tdder.gradle.build-number"
}

ext.pomName = "JUnit Teardown extension"
description = "JUnit Jupiter extension. Provides Automated Teardown mechanism."

repositories {
    mavenCentral()
}

dependencies {
    implementation platform(libs.junit.bom)
    implementation libs.junit.jupiter

    testImplementation libs.hamcrest
    testImplementation libs.junit.platform.launcher
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(SourceTask)
        .findAll { t -> t.hasProperty('options') }
        .findAll { t -> t.options.hasProperty('encoding') }
        .each { t -> t.options.encoding = 'UTF-8' }

tasks.withType(Javadoc).configureEach {
    options {
        charSet 'UTF-8'
        locale 'en'
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform() {
        excludeTags("used-from-test")
    }
}

tasks.withType(Jar).configureEach {
    dependsOn buildNumber
    doFirst {
        manifest {
            attributes["Archiver-Version"] = "Gradle ${gradle.gradleVersion}"
            attributes["Created-By"] = "Gradle"
            attributes["Built-By"] = "${System.getProperty("user.name")}"
            attributes["Build-Jdk"] = "${System.getProperty("java.version")} (${System.getProperty("java.vm.vendor")} ${System.getProperty("java.vm.version")})"
            attributes["Specification-Title"] = project.name
            attributes["Specification-Version"] = project.version
            attributes["Implementation-Title"] = project.name
            attributes["Implementation-Version"] = project.version
            attributes["Implementation-Vendor"] = project.group
            attributes["Built-Timestamp"] = (rootProject.ext.builtTimestamp as Instant).truncatedTo(ChronoUnit.SECONDS).toString()
            attributes["SCM-Revision"] = rootProject.ext.buildNumberValue
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = project.ext.pomName
                description = project.description
                url = "https://github.com/manhole/junit-teardown-extension"
                inceptionYear = "2023"
                licenses {
                    license {
                        name = "The Apache Software License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        id = "manhole"
                        name = "HONMA Hirotaka"
                        email = "manholex@gmail.com"
                    }
                }
                scm {
                    connection = "https://github.com/manhole/junit-teardown-extension.git"
                    developerConnection = "https://github.com/manhole/junit-teardown-extension.git"
                    url = "https://github.com/manhole/junit-teardown-extension"
                }
                issueManagement {
                    system = "GitHub"
                    url = "https://github.com/manhole/junit-teardown-extension/issues"
                }
            }
        }
    }
    repositories {
        maven {
            name "ProjectLocal"
            url project.layout.buildDirectory.dir('maven-repo').get().asFile.toURI().toURL()
        }
        maven {
            name = "sonatype"
            if (project.version.endsWith("-SNAPSHOT")) {
                url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            } else {
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            }
            credentials {
                username = project.findProperty("ossrh.username")
                password = project.findProperty("ossrh.password")
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

// https://qiita.com/cypher256/items/f3db6bba86692c3c7733
// @see org.gradle.api.tasks.wrapper.Wrapper, org.gradle.util.DistributionLocator
tasks.withType(Wrapper).configureEach {
    distributionType = Wrapper.DistributionType.ALL
    doFirst {
        def versionService = new URL('https://services.gradle.org/versions/current')
        gradleVersion = new groovy.json.JsonSlurper().parseText(versionService.text).version
        println "gradleVersion: ${gradleVersion}"
    }
}
